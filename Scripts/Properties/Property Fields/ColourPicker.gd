extends SelectionPropertyField
class_name ColourPickerPropertyField
## The [SelectionPropertyField] generated by [ColourPickerProperty].
##
## Allows the user to interact with the properties of [DraggableObject]s through the [SelectionMenu].
## [br]Executes the definition given by a [ColourPickerProperty].[br]
## [br]The function [member SelectionProperty.commandName] of the [member SelectionPropertyField.associatedObject] will be called when the
## [br] - [member colourpicker] emits [signal ColorPicker.color_changed]

#region Member Variables
## The [ColorPickerButton] for the [ColourPickerPropertyField]
var colourpicker : ColorPickerButton
#endregion

#region Initialization
## Class Initialization
func _init(obj : DraggableObject, property : ColourPickerProperty):
	super(obj, property) #calls SelectionPropertyField._init()
	
	#Note that PropertyField will run its initialization before this subclass
	# Create the colourpicker
	colourpicker = ColorPickerButton.new()
	colourpicker.color = property.defaultColour
	colourpicker.edit_alpha = property.alphaEnabled
	colourpicker.size_flags_horizontal = SIZE_EXPAND_FILL # fill up all the space after the name
	self.add_child(colourpicker)
	
	#Connect
	colourpicker.color_changed.connect(Callable(obj,property.onUpdate))
	# the color_changed signal will send the new Color to obj to handle
#endregion
